@Metadata.layer: #CUSTOMER
@UI.headerInfo: { typeNamePlural: 'Travels' , typeName: 'Travel', title: { type: #STANDARD, value: 'Description' }, description: { type: #STANDARD, value: 'TravelID' }}
annotate view ZC_FE_Travel_001350 with
{
  @UI.facet: [{ purpose: #HEADER, position: 10, type: #DATAPOINT_REFERENCE,
                targetQualifier: 'PriceData' },
              { purpose: #HEADER, position: 20, type: #DATAPOINT_REFERENCE,
                targetQualifier: 'StatusData' },
              { label: 'General Information', type:  #COLLECTION, id: 'GeneralInfo',
                position: 10 },
              { label: 'General', type: #IDENTIFICATION_REFERENCE, parentId: 'GeneralInfo' , purpose: #STANDARD,
                position: 10 },
              { label: 'Prices', purpose: #STANDARD, position: 20, type: #FIELDGROUP_REFERENCE,
                parentId: 'GeneralInfo', targetQualifier: 'PricesGroup' },
              { label: 'Dates', purpose: #STANDARD, position: 30, type: #FIELDGROUP_REFERENCE,
                parentId: 'GeneralInfo', targetQualifier: 'DatesGroup' },
                { position: 60, targetElement: '_Booking', label: 'Rating', type: #LINEITEM_REFERENCE }
              ]

  @UI.lineItem: [{ position: 10, dataAction: 'CancelTravel', 
                label: 'Cancel Travel', type: #FOR_ACTION  }]
  @UI.identification: [{ position: 10 }]
  TravelID;

  @UI.lineItem: [{ position: 20 }]
  @UI.selectionField: [{ position:10 }]
  @UI.identification: [{ position: 20 }]
  AgencyID;

  @UI.lineItem: [{ position: 30 }]
  @UI.selectionField: [{ position:20 }]
  @UI.identification: [{ position: 30 }]
  CustomerID;

  @UI.lineItem: [{ position: 40 }]
  @UI.fieldGroup: [{ qualifier: 'DatesGroup', position: 10 }]
  BeginDate;

  @UI.lineItem: [{ position: 50 }]
  @UI.fieldGroup: [{ qualifier: 'DatesGroup', position: 0 }]
  EndDate;
  

  @UI.lineItem: [{ position: 80 }]
  @UI.fieldGroup: [{ qualifier: 'PricesGroup', position: 10 }]
  BookingFee;

  @UI.lineItem: [{ position: 70 }]
  @UI.dataPoint: { qualifier: 'PriceData', title: 'Total Price' }
  @UI.fieldGroup: [{ qualifier: 'PricesGroup', position: 20}]
  TotalPrice;

  @UI.lineItem: [{ position: 60 /*  Add the criticality only if you added the corresponding code in the projection view, see the corresponding exercise*/}]
  @UI.selectionField: [{ position:30 }]
  @UI.textArrangement: #TEXT_ONLY
  @UI.dataPoint: { qualifier: 'StatusData', title: 'Status' /*  Add the criticality only if you added the corresponding code in the projection view, see the corresponding exercise*/ }
  OverallStatus;

  @UI.lineItem: [{ position: 90 }]
  LastChangedAt;

}